config("compiler_defaults") {
  defines = []
  if (is_debug) {
    defines += [
      "DEBUG",
      "_DEBUG",
    ]
  } else {
    defines += [ "NDEBUG" ]
  }
  if (is_win) {
    defines += [
      "UNICODE",
      "_UNICODE",
    ]
  }

  cflags = []
  if (is_posix) {
    cflags += [
      "-g",
      "-fvisibility=hidden",
    ]
    if (is_debug) {
      cflags += [
        "-O0",
        "-Og",
      ]
    } else {
      cflags += [ "-Ofast" ]
    }
    if (is_linux) {
      cflags += [
        "-fPIC",
        "-pthread",
      ]
    }
  }
  if (is_win) {
    cflags += [
      "/Zi",
      "/Zl",
      "/bigobj",
      "/Oy-",
      "/Gm-",
      "/GS",
      "/fp:precise",
      "/Gd",
      "/Zc:inline",
    ]
    if (is_debug) {
      cflags += [
        "/Od",
        "/RTC1",
      ]
    } else {
      cflags += [
        "/O2",
        "/Oi",
        "/Gy",
        "/GL",
      ]
    }
  }

  cflags_cc = []
  if (is_win) {
    cflags_cc += [ "/EHsc" ]
  }

  ldflags = []
  if (is_win) {
    ldflags += [ "/DEBUG" ]
    if (target_cpu == "x64") {
      ldflags += [ "/MACHINE:X64" ]
    } else if (target_cpu == "x86") {
      ldflags += [ "/MACHINE:X86" ]
    }
    if (!is_debug) {
      ldflags += [
        "/OPT:REF",
        "/OPT:ICF",
        "/LTCG:incremental",
      ]
    }
  }
}
config("executable_ldconfig") {
  ldflags = []
  if (is_posix) {
    if (!is_apple) {
      ldflags += [
        "-Wl,-rpath=\$ORIGIN/",
        "-Wl,-rpath-link=",
      ]
    }
  }
}
