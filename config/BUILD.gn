config("compiler_defaults") {
  defines = []
  if (is_debug) {
    defines += [
      "DEBUG",
      "_DEBUG",
    ]
  } else {
    defines += [ "NDEBUG" ]
  }
  if (is_win) {
    defines += [
      "UNICODE",
      "_UNICODE",
    ]
  }

  cflags = []
  if (is_posix) {
    cflags += [
      "-g",
      "-fvisibility=hidden",
    ]
    if (is_debug) {
      cflags += [
        "-O0",
        "-Og",
      ]
    } else {
      cflags += [ "-Ofast" ]
    }
    if (is_linux) {
      cflags += [
        "-fPIC",
        "-pthread",
      ]
    }
  }
  if (is_win) {
    cflags += [
      "/analyze-",
      "/bigobj",
      "/EHsc",
      "/fp:precise",
      "/Gd",
      "/Gm-",
      "/GS",
      "/Oy-",
      "/W3",
      "/WX-",
      "/Zc:forScope",
      "/Zc:inline",
      "/Zc:wchar_t",
      "/Zl",
    ]
    if (is_debug) {
      cflags += [
        "/JMC",
        "/Od",
        "/RTC1",
        "/ZI",
      ]
    } else {
      cflags += [
        "/GL",
        "/Gy",
        "/O2",
        "/Oi",
        "/Zi",
      ]
    }
  }

  cflags_cc = []
  if (is_win) {
    cflags_cc += [ "/EHsc" ]
  }

  ldflags = []
  if (is_win) {
    ldflags += [
      "/DEBUG",
      "/MANIFEST:EMBED",
    ]
    if (target_cpu == "x64") {
      ldflags += [ "/MACHINE:X64" ]
    } else if (target_cpu == "x86") {
      ldflags += [ "/MACHINE:X86" ]
    }
    if (!is_debug) {
      ldflags += [
        "/INCREMENTAL:NO",
        "/LTCG:incremental",
        "/OPT:ICF",
        "/OPT:REF",
        "/SAFESEH",
      ]
    }
  }
}
config("executable_ldconfig") {
  ldflags = []
  if (is_posix) {
    if (!is_apple) {
      ldflags += [
        "-Wl,-rpath=\$ORIGIN/",
        "-Wl,-rpath-link=",
      ]
    }
  }
}
