# =============================================================================
# Dynamic or static link CRT
# =============================================================================

config("dynamic_runtime") {
  cflags = []
  libs = [ "oldnames.lib" ]
  if (is_debug) {
    cflags += [ "/MDd" ]
    libs += [
      "ucrtd.lib",
      "msvcrtd.lib",
      "msvcprtd.lib",
      "vcruntimed.lib",
    ]
  } else {
    cflags += [ "/MD" ]
    libs += [
      "ucrt.lib",
      "msvcrt.lib",
      "msvcprt.lib",
      "vcruntime.lib",
    ]
  }
}

config("static_runtime") {
  cflags = []
  libs = [ "oldnames.lib" ]
  if (is_debug) {
    cflags += [ "/MTd" ]
    libs += [
      "libucrtd.lib",
      "libcmtd.lib",
      "libcpmtd.lib",
      "libvcruntimed.lib",
    ]
  } else {
    cflags += [ "/MT" ]
    libs += [
      "libucrt.lib",
      "libcmt.lib",
      "libcpmt.lib",
      "libvcruntime.lib",
    ]
  }
}

# =============================================================================
# SUBSYSTEM: Console or Windows
# =============================================================================

config("console_subsystem") {
  ldflags = [ "/SUBSYSTEM:CONSOLE" ]
}

config("windows_subsystem") {
  ldflags = [ "/SUBSYSTEM:WINDOWS" ]
}

# =============================================================================
# Windows XP Support
# =============================================================================

config("console_subsystem_xp") {
  ldflags = [ "/SUBSYSTEM:CONSOLE,5.02" ]
}

config("windows_subsystem_xp") {
  ldflags = [ "/SUBSYSTEM:WINDOWS,5.02" ]
}
